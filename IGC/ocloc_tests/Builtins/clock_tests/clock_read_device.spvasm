
;=========================== begin_copyright_notice ============================
;
; Copyright (C) 2025 Intel Corporation
;
; SPDX-License-Identifier: MIT
;
;============================ end_copyright_notice =============================

; REQUIRES: spirv-as, regkeys, pvc-supported
; RUN: spirv-as --target-env spv1.0 -o %t.spv %s
; RUN: ocloc compile -spirv_input -file %t.spv -device pvc -options " -igc_opts 'PrintToConsole=1 PrintAfter=BuiltinsConverterFunction'" 2>&1 | FileCheck %s
; CHECK: @llvm.genx.GenISA.cycleCounter()

; Test: OpReadClockKHR with device scope generates GenISA.cycleCounter intrinsic
               OpCapability Addresses
               OpCapability Linkage
               OpCapability Kernel
               OpCapability Int64
               OpCapability ShaderClockKHR
               OpExtension "SPV_KHR_shader_clock"
               OpMemoryModel Physical64 OpenCL
               OpEntryPoint Kernel %clock_sub_group "clock_read_sub_group"
               OpDecorate %buf Alignment 4
      %ulong = OpTypeInt 64 0
       %uint = OpTypeInt 32 0
     %uint_0 = OpConstant %uint 0
     %uint_1 = OpConstant %uint 1
    %ulong_1 = OpConstant %ulong 1
    %ulong_2 = OpConstant %ulong 2
    %ulong_3 = OpConstant %ulong 3
%scope_device = OpConstant %uint 1
       %void = OpTypeVoid
%_ptr_CrossWorkgroup_uint = OpTypePointer CrossWorkgroup %uint
   %functype = OpTypeFunction %void %_ptr_CrossWorkgroup_uint
       %bool = OpTypeBool
%clock_sub_group = OpFunction %void None %functype
        %buf = OpFunctionParameter %_ptr_CrossWorkgroup_uint
  %funcstart = OpLabel
      %buf_1 = OpInBoundsPtrAccessChain %_ptr_CrossWorkgroup_uint %buf %ulong_1
      %buf_2 = OpInBoundsPtrAccessChain %_ptr_CrossWorkgroup_uint %buf %ulong_2
      %buf_3 = OpInBoundsPtrAccessChain %_ptr_CrossWorkgroup_uint %buf %ulong_3
      %start = OpReadClockKHR %ulong %scope_device
               OpBranch %looptop
    %looptop = OpLabel
          %i = OpPhi %uint %uint_0 %funcstart %next_i %looptop
      %data0 = OpLoad %uint %buf Volatile|Aligned 4
      %data1 = OpLoad %uint %buf_1 Volatile|Aligned 4
        %sum = OpIAdd %uint %data0 %data1
               OpStore %buf %sum Volatile|Aligned 4
     %next_i = OpIAdd %uint %i %uint_1
      %data2 = OpLoad %uint %buf_2 Volatile|Aligned 4
   %looptest = OpULessThan %bool %next_i %data2
               OpBranchConditional %looptest %looptop %loopend
    %loopend = OpLabel
        %end = OpReadClockKHR %ulong %scope_device
       %test = OpULessThan %bool %end %start
               OpBranchConditional %test %funcend %writepass
  %writepass = OpLabel
               OpStore %buf_3 %uint_1 Volatile|Aligned 4
               OpBranch %funcend
    %funcend = OpLabel
               OpReturn
               OpFunctionEnd
