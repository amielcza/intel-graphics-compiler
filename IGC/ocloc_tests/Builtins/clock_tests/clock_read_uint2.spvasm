
;=========================== begin_copyright_notice ============================
;
; Copyright (C) 2025 Intel Corporation
;
; SPDX-License-Identifier: MIT
;
;============================ end_copyright_notice =============================

; REQUIRES: spirv-as, regkeys, pvc-supported
; RUN: spirv-as --target-env spv1.0 -o %t.spv %s
; RUN: ocloc compile -spirv_input -file %t.spv -device pvc -options " -igc_opts 'PrintToConsole=1 PrintAfter=BuiltinsConverterFunction'" 2>&1 | FileCheck %s
; CHECK: @llvm.genx.GenISA.cycleCounter()

; Test case for ReadClockKHR returning uint2
               OpCapability Addresses
               OpCapability Linkage
               OpCapability Kernel
               OpCapability Int64
               OpCapability ShaderClockKHR
               OpExtension "SPV_KHR_shader_clock"
               OpMemoryModel Physical64 OpenCL
               OpEntryPoint Kernel %clock_uint2_test "clock_read_uint2_test"
               OpDecorate %buf Alignment 4
      %ulong = OpTypeInt 64 0
       %uint = OpTypeInt 32 0
      %uint2 = OpTypeVector %uint 2
     %uint_0 = OpConstant %uint 0
     %uint_1 = OpConstant %uint 1
     %uint_2 = OpConstant %uint 2
    %ulong_1 = OpConstant %ulong 1
    %ulong_2 = OpConstant %ulong 2
%scope_device = OpConstant %uint 1
       %void = OpTypeVoid
%_ptr_CrossWorkgroup_uint = OpTypePointer CrossWorkgroup %uint
   %functype = OpTypeFunction %void %_ptr_CrossWorkgroup_uint
%clock_uint2_test = OpFunction %void None %functype
        %buf = OpFunctionParameter %_ptr_CrossWorkgroup_uint
  %funcstart = OpLabel
      %buf_1 = OpInBoundsPtrAccessChain %_ptr_CrossWorkgroup_uint %buf %ulong_1
      %buf_2 = OpInBoundsPtrAccessChain %_ptr_CrossWorkgroup_uint %buf %ulong_2
      %clock = OpReadClockKHR %uint2 %scope_device
    %clock_x = OpCompositeExtract %uint %clock 0
    %clock_y = OpCompositeExtract %uint %clock 1
               OpStore %buf %clock_x Volatile|Aligned 4
               OpStore %buf_1 %clock_y Volatile|Aligned 4
               OpStore %buf_2 %uint_1 Volatile|Aligned 4
               OpReturn
               OpFunctionEnd
