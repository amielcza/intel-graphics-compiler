; REQUIRES: regkeys, spirv-as, dg2-supported, llvm-16-plus

; RUN: spirv-as --target-env spv1.0 -o %t.spv %s

; LLVM with opaque pointers:
; RUN: ocloc compile -spirv_input -file %t.spv -device dg2 -options " -igc_opts 'EnableOpaquePointersBackend=1,ShaderDumpTranslationOnly=1'" 2>&1 | FileCheck %s
; LLVM with typed pointers/default pointer typing:
; RUN: ocloc compile -spirv_input -file %t.spv -device dg2 -options " -igc_opts 'ShaderDumpTranslationOnly=1'" 2>&1 | FileCheck %s

               OpCapability Addresses
               OpCapability Kernel
               OpCapability Int64
               OpMemoryModel Physical32 OpenCL
               OpEntryPoint Kernel %1 "testNarrowingUToPtr"
               OpName %a "a"
               OpName %entry "entry"
       %uint = OpTypeInt 32 0
     %uint_0 = OpConstant %uint 0
      %ulong = OpTypeInt 64 0
       %void = OpTypeVoid
%_ptr_CrossWorkgroup_uint = OpTypePointer CrossWorkgroup %uint
          %9 = OpTypeFunction %void %ulong
          %1 = OpFunction %void None %9
          %a = OpFunctionParameter %ulong
      %entry = OpLabel
         %10 = OpConvertUToPtr %_ptr_CrossWorkgroup_uint %a
               OpStore %10 %uint_0 Aligned 4
               OpReturn
               OpFunctionEnd

; CHECK: inttoptr i64 %a to {{ptr|i32}} addrspace(1)
